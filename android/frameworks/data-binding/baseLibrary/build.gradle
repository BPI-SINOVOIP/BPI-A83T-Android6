/*
 * Copyright (C) 2014 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = config.javaTargetCompatibility
targetCompatibility = config.javaSourceCompatibility

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
}

def javadocTask = project.tasks.create(name: "javadocBaseLibrary", type: Javadoc) {
    source sourceSets.main.allJava
}

def javadocJarTask = project.tasks.create(name: "javadocJarBaseLibrary", type: Jar) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}
javadocJarTask.dependsOn javadocTask

def sourcesJarTask = project.tasks.create(name: "sourceJarBaseLibrary", type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}

artifacts.add('archives', javadocJarTask);
artifacts.add('archives', sourcesJarTask);


uploadArchives {
    repositories {
        mavenDeployer {
            pom.artifactId = 'baseLibrary'
            pom.project {
                licenses {
                    license {
                        name config.licenseName
                        url config.licenseUrl
                        distribution config.licenseDistribution
                    }
                }
            }
        }
    }
}

task prebuildJar(type : Copy) {
    dependsOn uploadArchives
    from "$buildDir/libs/baseLibrary-${version}.jar"
    into config.prebuildFolder
    rename { String fileName ->
        "databinding-baseLibrary.jar"
    }
}